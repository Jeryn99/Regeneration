buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = project.mod_version
group = project.maven_group
archivesBaseName = project.jar_name

sourceCompatibility = targetCompatibility = 1.8

minecraft {
	version = "${project.minecraft_verison}-${project.forge_version}"
	mappings = project.mappings_version
	makeObfSourceJar = true
	
	if(project.hasProperty('runDir'))
		runDir = project.runDir
	else if(file('../run').exists())
		runDir = "../run"
	else
		runDir = "run"
}

repositories {
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven"
	}
	
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
	}

	maven {
		name = "DarkRoleplayMaven"
		url = "https://dark-roleplay.net/files/maven/"
	}
}

configurations {
	// configuration that holds jars to embed inside the jar
	embed
	compile.extendsFrom(embed)
}

dependencies {
	embed "darkroleplay.core_modules:drpcmguis:1.12.2-0.0.1:pre"
	embed "darkroleplay:drplibrary:1.12.2-0.1.2.3:pre"
}


jar{
	into('META-INF/libraries'){
		from configurations.embed
	}

	manifest {
		attributes([
				'FMLAT': "drpcore_at.cfg",
				'ContainedDeps': configurations.embed.collect { it.getName() }.join(' '),
				'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
				'Timestamp': System.currentTimeMillis()
		])
	}
}


processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}